
#Область Константы

Перем кВидыСимволов;

Процедура Инициализировать()
	Перем Алфавит, Номер;
	кВидыСимволов = Новый Соответствие;
	Алфавит = (
		"abcdefghijklmnopqrstuvwxyzABCDEFGHIJKLMNOPQRSTUVWXYZ_=+-*/<>%!?" +
		"абвгдеёжзийклмнопрстуфхцчшщъыьэюяАБВГДЕЁЖЗИЙКЛМНОПРСТУФХЦЧШЩЪЫЬЭЮЯ"
	);
	Для Номер = 1 По СтрДлина(Алфавит) Цикл
		кВидыСимволов[Сред(Алфавит, Номер, 1)] = "Буква"
	КонецЦикла;
	Для Номер = 0 По 9 Цикл
		кВидыСимволов[Строка(Номер)] = "Цифра"
	КонецЦикла;
КонецПроцедуры // Инициализировать()

#КонецОбласти // Константы

#Область Парсер

Функция Парсер(Знач Исходник) Экспорт
	Возврат Новый Структура(
		"Исходник,"
		"Позиция,"
		"Символ,"
		"Окружение",
		Исходник, 1, Сред(Исходник, 1, 1)
	);
КонецФункции // Парсер()

Функция ПрочитатьСимвол(Знач Исходник, Позиция)
	Позиция = Позиция + 1;
	Возврат Сред(Исходник, Позиция, 1);
КонецФункции // ПрочитатьСимвол()

Процедура Следующий(Знач Парсер, Токен, Литерал)
	Перем Исходник, Позиция, Символ, Начало;
	Исходник = Парсер.Исходник;
	Позиция = Парсер.Позиция;
	Символ = Парсер.Символ;
	Пока ПустаяСтрока(Символ) И Символ <> "" Цикл
		Символ = ПрочитатьСимвол(Исходник, Позиция);
	КонецЦикла;
	Токен = кВидыСимволов[Символ];
	Если Токен = "Буква" Тогда
		Начало = Позиция;
		Символ = ПрочитатьСимвол(Исходник, Позиция);
		Пока кВидыСимволов[Символ] <> Неопределено Цикл
			Символ = ПрочитатьСимвол(Исходник, Позиция);
		КонецЦикла;
		Литерал = Сред(Парсер.Исходник, Начало, Позиция - Начало);
		Токен = "Объект";
	ИначеЕсли Токен = "Цифра" Тогда
		Начало = Позиция;
		Символ = ПрочитатьСимвол(Исходник, Позиция);
		Пока кВидыСимволов[Символ] = "Цифра" Цикл
			Символ = ПрочитатьСимвол(Исходник, Позиция);
		КонецЦикла;
		Если Символ = "." Тогда
			Символ = ПрочитатьСимвол(Исходник, Позиция);
			Пока кВидыСимволов[Символ] = "Цифра" Цикл
				Символ = ПрочитатьСимвол(Исходник, Позиция);
			КонецЦикла;
		КонецЕсли;
		Литерал = Сред(Парсер.Исходник, Начало, Позиция - Начало);
		Токен = "Число";
	ИначеЕсли Символ = """" Тогда
		Начало = Позиция + 1;
		Символ = ПрочитатьСимвол(Исходник, Позиция);
		Пока Символ <> """" И Символ <> "" Цикл
			Символ = ПрочитатьСимвол(Исходник, Позиция);
		КонецЦикла;
		Литерал = Сред(Парсер.Исходник, Начало, Позиция - Начало);
		Токен = "Строка";
		Символ = ПрочитатьСимвол(Исходник, Позиция);
	ИначеЕсли Символ = "(" Или Символ = ")" Или Символ = "" Тогда
		Токен = Символ;
		Символ = ПрочитатьСимвол(Исходник, Позиция);
	Иначе
		ВызватьИсключение СтрШаблон("Неизвестный символ %1", Символ);
	КонецЕсли;
	Парсер.Позиция = Позиция;
	Парсер.Символ = Символ;
КонецПроцедуры // Следующий()

Функция Список(Знач Тип, Знач Данные, Знач Следующий)
	Возврат Новый ФиксированнаяСтруктура("Тип, Данные, Следующий", Тип, Данные, Следующий);
КонецФункции // Список()

Функция Разобрать(Знач Парсер, Уровень = 0) Экспорт
	Перем Токен, Литерал;
	Следующий(Парсер, Токен, Литерал);
	Если Токен = "(" Тогда
		Уровень = Уровень + 1;
		Возврат Список("Список", Разобрать(Парсер, Уровень), Разобрать(Парсер, Уровень));
	ИначеЕсли Токен = ")" Тогда
		Если Уровень = 0 Тогда
			ВызватьИсключение "Неожиданный символ `)`";
		КонецЕсли;
		Уровень = Уровень - 1;
	ИначеЕсли Токен = "" Тогда
		Если Уровень > 0 Тогда
			ВызватьИсключение "Ожидается `)`";
		КонецЕсли;
	Иначе
		Возврат Список(Токен, Литерал, Разобрать(Парсер, Уровень))
	КонецЕсли;
	Возврат Неопределено;
КонецФункции // Разобрать()

#КонецОбласти // Парсер

#Область Окружение

Функция ОткрытьОкружение(Окружение) Экспорт
	Окружение = Новый Структура("ВнешнееОкружение, Элементы", Окружение, Новый Соответствие);
КонецФункции // ОткрытьОкружение()

Функция ЗакрытьОкружение(Окружение) Экспорт
	Окружение = Окружение.ВнешнееОкружение;
КонецФункции // ЗакрытьОкружение()

Функция ЭлементОкружения(Знач Окружение, Знач ИмяЭлемента) Экспорт
	Перем Элемент;
	Элемент = Окружение.Элементы[ИмяЭлемента];
	Пока Элемент = Неопределено И Окружение.ВнешнееОкружение <> Неопределено Цикл
		Окружение = Окружение.ВнешнееОкружение;
		Элемент = Окружение.Элементы[ИмяЭлемента];
	КонецЦикла;
	Если Элемент = Неопределено Тогда
		ВызватьИсключение СтрШаблон("Неизвестный атом %1", ИмяЭлемента);
	КонецЕсли;
	Возврат Элемент;
КонецФункции // ЭлементОкружения()

#КонецОбласти // Окружение

Функция Интерпретировать(Знач Окружение, Знач Список) Экспорт
	Перем Тип, Данные;
	Тип = Список.Тип; Данные = Список.Данные; 
	Если Тип = "Объект" Тогда
		Если Данные = "Функция" Тогда
			ОпределениеФункции = Список.Следующий;
			Окружение.Элементы[ОпределениеФункции.Данные] = ОпределениеФункции.Следующий;
		ИначеЕсли Данные = "+" Тогда
			Возврат Интерпретировать(Окружение, Список.Следующий) + Интерпретировать(Окружение, Список.Следующий.Следующий);
		ИначеЕсли Данные = "-" Тогда
			Возврат Интерпретировать(Окружение, Список.Следующий) - Интерпретировать(Окружение, Список.Следующий.Следующий);
		ИначеЕсли Данные = "*" Тогда
			Возврат Интерпретировать(Окружение, Список.Следующий) * Интерпретировать(Окружение, Список.Следующий.Следующий);
		ИначеЕсли Данные = "/" Тогда
			Возврат Интерпретировать(Окружение, Список.Следующий) / Интерпретировать(Окружение, Список.Следующий.Следующий);
		ИначеЕсли Данные = "%" Тогда
			Возврат Интерпретировать(Окружение, Список.Следующий) % Интерпретировать(Окружение, Список.Следующий.Следующий);
		Иначе
			ЭлементОкружения = ЭлементОкружения(Окружение, Список.Данные);
			Если ТипЗнч(ЭлементОкружения) = Тип("ФиксированнаяСтруктура") Тогда
				ОткрытьОкружение(Окружение);
				Аргументы = Список.Следующий;
				Параметры = ЭлементОкружения.Данные;
				Пока Параметры <> Неопределено Цикл
					Если Аргументы = Неопределено Тогда
						ВызватьИсключение "Недостаточно фактических параметров";
					КонецЕсли; 
					Окружение.Элементы[Параметры.Данные] = Интерпретировать(Окружение, Аргументы);
					Параметры = Параметры.Следующий;
					Аргументы = Аргументы.Следующий;
				КонецЦикла; 
				Выражение = ЭлементОкружения.Следующий.Данные;
				Результат = Интерпретировать(Окружение, Выражение);
				ЗакрытьОкружение(Окружение);
				Возврат Результат;
			Иначе
				Возврат ЭлементОкружения;
			КонецЕсли; 
		КонецЕсли;
	ИначеЕсли Тип = "Число" Тогда
		Возврат Число(Данные);
	ИначеЕсли Тип = "Строка" Тогда
		Возврат Данные;
	Иначе // Список
		Возврат Интерпретировать(Окружение, Данные);	
	КонецЕсли;
КонецФункции // Интерпретировать()

Функция Пуск(Знач Исходник) Экспорт
	Перем Парсер, Список, Результат;
	Парсер = Парсер(Исходник);
	Список = Разобрать(Парсер);
	Результат = Новый Массив;
	ОткрытьОкружение(Парсер.Окружение);
	Пока Список <> Неопределено Цикл
		Значение = Интерпретировать(Парсер.Окружение, Список.Данные);
		Если Значение <> Неопределено Тогда
			Результат.Добавить(Значение);
		КонецЕсли; 
		Список = Список.Следующий;
	КонецЦикла;
	ЗакрытьОкружение(Парсер.Окружение);
	Возврат СтрСоединить(Результат, Символы.ПС);
КонецФункции // Пуск()

Инициализировать();